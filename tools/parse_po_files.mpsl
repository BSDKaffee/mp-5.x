/*

	Parses .po files and dumps MPSL code.

	Angel Ortega <angel@triptico.com>

*/

sub convert_po_line(line)
{
	line = sregex('/^\s*"/', line);
	line = sregex('/"$/', line);

	line = split(NULL, line);

	line = join('',
			map(sub (e) {
				(ord(e) > 127) &&
				sprintf("\\x{%04x}", ord(e)) ||
				e ;
			},
			line
		)
	);

	return line;
}


sub parse_po_file(pofile, lang)
{
	local f;
	local r = [];

	/* only UTF-8 po files are supported */
	encoding('UTF-8');

	if ((f = open(pofile, "r")) != NULL) {

		local l, k;
		local v = [];

		push(r, "__I18N__ = {");

		while ((l = read(f)) != NULL) {

			l = mp.chomp(l);

			local s = regex([ '/^(msgid|msgstr)*/', '/\s*\".*\"$/' ], l);
			s[1] = convert_po_line(s[1]);

			if (s[0] eq 'msgid') {
				if (k) {
					k = grep(sub (e) { e != NULL; }, k);
					v = grep(sub (e) { e != NULL; }, v);

					if (size(k) && size(v)) {
						push(r, "\t\"" ~
							join('', k) ~
							"\" => \"" ~
							join('', v) ~
							"\","
						);
					}

					k = NULL;
				}

				v = [ s[1] ];
			}
			else
			if (s[0] eq 'msgstr') {
				k = v;
				v = [ s[1] ];
			}
			else
			if (s[1]) {
				push(v, s[1]);
			}
		}

		push(r, "\t\"LANG\" => \"" ~ lang ~ "\"\n};");

		close(f);
	}

	/* back to default encoding */
	encoding();

	return join("\n", r);
}
