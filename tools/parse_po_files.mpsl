/*

	Parses .po files and dumps MPSL code.

	Angel Ortega <angel@triptico.com>

*/

/* files */
local files = glob('po/*.po');

/* only UTF-8 po files are supported */
encoding('UTF-8');

foreach(pofile, files) {
	local f;

	if ((f = open(pofile, "r")) != NULL) {

		local l, k;
		local v = [];
		local h = {};

		/* strip language name */
		local lang = regex([ '/\//', '/\w+/', '/\.po$/' ], pofile);
		lang = lang[1];

		while ((l = read(f)) != NULL) {

			l = mp.chomp(l);

			local s = regex([ '/^(msgid|msgstr)*/', '/\s*\".*\"$/' ], l);

			if (s[0] eq 'msgid') {
				if (k) {
					h[k] = v;
					k = NULL;
				}

				v = [ s[1] ];
			}
			else
			if (s[0] eq 'msgstr') {
				k = v;
				v = [ s[1] ];
			}
			else
			if (s[1]) {
				push(v, s[1]);
			}
		}

		close(f);
		dump(h);
	}
}

/* back to default encoding */
encoding();
