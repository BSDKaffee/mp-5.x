/*

    Minimum Profit 5.x
    A Programmer's Text Editor

    Copyright (C) 1991-2006 Angel Ortega <angel@triptico.com>

    This program is free software; you can redistribute it and/or
    modify it under the terms of the GNU General Public License
    as published by the Free Software Foundation; either version 2
    of the License, or (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.

    http://www.triptico.com

*/

/*******************
	Data
********************/

/* configuration */
mp.config = {};
mp.config.eol = "\n";
mp.config.undo_levels = 20;
mp.config.word_wrap = 0;
mp.config.auto_indent = 0;
mp.config.status_format = "%m%n %x,%y [%l] %R%O %s %t";
mp.config.tab_size = 8;
mp.config.tabs_as_spaces = 0;

/* a regex for selection words */
mp.word_regex = '/[A-Z0-9_]+/i';

/* document list */
mp.docs = [];
mp.active_i = 0;

/* status line directives */
mp.status_f = {};

/* allowed color names (order matters, match the Unix curses one) */
mp.color_names = [ "default", "black", "red", "green",
	"yellow", "blue", "magenta", "cyan", "white" ];

/* color definitions */
mp.colors = {
	'normal' => {		'text'	=> [ 'default', 'default' ],
				'gui'	=> [ 0x000000, 0xffffff ] },
	'cursor' => {		'text'	=> [ 'default', 'default' ],
				'gui'	=> [ 0x000000, 0xffffff ],
				'flags' => [ 'reverse' ] },
	'selection' => {	'text'	=> [ 'red', 'default' ],
				'gui'	=> [ 0xff0000, 0xffffff ],
				'flags'	=> [ 'reverse'] },
	'comments' => {		'text'	=> [ 'green', 'default' ],
				'gui'	=> [ 0x00cc77, 0xffffff ] },
	'quotes' => {		'text'	=> [ 'blue', 'default' ],
				'gui'	=> [ 0x0000ff, 0xffffff ],
				'flags'	=> [ 'bright' ] },
	'matching' => {		'text'	=> [ 'black', 'cyan' ],
				'gui'	=> [ 0x000000, 0xffff00 ] },
	'word1' => {		'text'	=> [ 'green', 'default' ],
				'gui'	=> [ 0x00aa00, 0xffffff ],
				'flags'	=> [ 'bright' ] },
	'word2' => {		'text'	=> [ 'red', 'default' ],
				'gui'	=> [ 0xff6666, 0xffffff ],
				'flags' => [ 'bright' ] },
	'tag' => {		'text'	=> [ 'cyan', 'default' ],
				'gui'	=> [ 0x8888ff, 0xffffff ],
				'flags'	=> [ 'bright', 'underline' ] },
	'spell' => {		'text'	=> [ 'red', 'default' ],
				'gui'	=> [ 0xff8888, 0xffffff ],
				'flags'	=> [ 'bright', 'underline' ] }
};

/* hash of specially coloured words */
mp.word_color = {};

mp.keycodes = {
	'ctrl-f5'	=>	sub (d) { mp.insert(d, "1968-08-17"); },

	'ctrl-a'	=>	NULL,
	'ctrl-b'	=>	NULL,
	'ctrl-d'	=>	NULL,
	'ctrl-e'	=>	NULL,
	'ctrl-h'	=>	NULL,
	'ctrl-j'	=>	NULL,
	'ctrl-k'	=>	NULL,
	'ctrl-l'	=>	NULL,
	'ctrl-p'	=>	NULL,
	'ctrl-t'	=>	NULL,
	'ctrl-u'	=>	NULL
};

mp.actions = {};

mp.actdesc = {};

/*******************
	Code
********************/

/* L(x) is the same as gettext(x) */
L = gettext;

/* LL(x) is the same as x */
sub LL(x) { x; }

sub mp.active()
/* returns the active document */
{
	local d;

	/* empty document list? create a new, empty one */
	if(size(mp.docs) == 0)
		mp.new();

	/* get active document */
	d = mp.docs[mp.active_i];

	return(d);
}


sub mp.process_event(k)
/* processes a key event */
{
	local d, f;

	d = mp.active();

	/* get the action asociated to the keycode */
	if((f = mp.keycodes[k]) != NULL)
	{
		/* if it's not code, resolve to an action */
		if(!is_exec(f))
			f = mp.actions[f];

		/* execute, if possible */
		if(is_exec(f))
			f(d);
	}
	else
		mp.insert(d, k);
}


sub mp.build_status_line()
/* returns the string to be drawn in the status line */
{
	local d = mp.active();
	local w = mp.get_word(d);
	local t = mp.tags[w].label;

	mp.status_f = {
		"%V"	=>	mp.VERSION,		/* version */
		"%m"	=>	d.txt.mod && "*" || "",	/* modify flag */
		"%x"	=>	d.txt.x + 1,		/* x coord */
		"%y"	=>	d.txt.y + 1,		/* y coord */
		"%l"	=>	size(d.txt.lines),	/* # of lines */
		"%R"	=>	"",			/* recording flag */
		"%O"	=>	"",			/* overwrite mode */
		"%s"	=>	d.syntax.name,		/* synhi mode */
		"%t"	=>	t,			/* tag target */
		"%n"	=>	d.name,			/* document name */
		"%%"	=>	"%"			/* escaped % */
	};

	return(sregex("/%./g", mp.config.status_format,
		sub (k) { mp.status_f[k]; }));
}


sub mp.get_history(key)
/* returns a history for the specified key */
{
	if(key == NULL) return(NULL);
	if(mp.history == NULL) mp.history = {};
	if(mp.history[key] == NULL) mp.history[key] = [];

	return(mp.history[key]);
}


sub mp.process_cmdline()
/* process the command line arguments (ARGV) */
{
	local o, line;

	if(size(ARGV) <= 1)
		return;

	/* skip ARGV[0] */
	shift(ARGV);

	while(o = shift(ARGV))
	{
		if(o eq '-e')
		{
			/* execute code */
			eval(shift(ARGV));

			if(ERROR != NULL)
				print(ERROR, "\n");
		}
		else
		if(regex('/^\+/', o))
		{
			/* move to line */
			line = o - 1;
		}
		else
			mp.open(o);
	}

	/* set the first as the active one */
	mp.active_i = 0;

	/* if there is a line defined, move there */
	if(line != NULL)
		mp.set_y(mp.active(), line);
}


sub mp.load_profile()
/* loads ~/.mp.mpsl */
{
/*	local t = INC;*/

	/* overwrites INC, putting just ~ */
/*	INC = [ mp.HOME ];*/

	/* execute */
/*	eval(sub { load(".mp.mpsl"); });*/

	/* gets INC back */
/*	INC = t;*/

	eval( sub {
		local INC = [ HOMEDIR ];
		load(".mp.mpsl");
	});
}


sub mp.ui()
/* wrapper for mp.drv.ui */
{
	mp.drv.ui();
}


load("mp_move.mpsl");
load("mp_edit.mpsl");
load("mp_file.mpsl");
load("mp_clipboard.mpsl");
load("mp_search.mpsl");
load("mp_tags.mpsl");
load("mp_syntax.mpsl");
load("mp_macro.mpsl");
load("mp_templates.mpsl");
load("mp_misc.mpsl");

/** MAIN **/

mp.process_cmdline();
mp.load_profile();
mp.ui();
