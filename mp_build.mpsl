/*

    Minimum Profit - A Text Editor
    A simple IDE-like build system.

    Angel Ortega <angel@triptico.com> et al.

    This software is released into the public domain.
    NO WARRANTY. See file LICENSE for details.

*/

mp.config.make_opts = '';

/** editor actions **/

mp.actions['build']	= sub {
    local t, target;

    if ((t = mp.build_get_targets()) == NULL)
        mp.alert(L("No build targets found."));
    else {
        /* more than one target? ask user to pick one */
        if (size(t) > 1) {
            local r = mp.form(
                [
                    {
                        label:  L("Build target") ~ ':',
                        type:   'list',
                        list:   t,
                        value:  mp.makefile_target
                    }
                ]
            );

            if (r != NULL) {
                mp.makefile_target = r[0];
                target = t[mp.makefile_target];
            }
        }

        if (target != NULL)
            mp.long_op(mp.build_execute, target);
    }
};


/** Default key bindings **/

mp.keycodes['f2'] = "build";

/** action descriptions **/

mp.actdesc['build']             = LL("Build project...");


/** code **/

sub mp.build_get_targets()
{
    local r, f;
    local m = 'make';

    if (mp.config.make_opts)
        m ~= ' ' ~ mp.config.make_opts;

    if ((f = open('Makefile', 'r')) != NULL) {
        local l;

        r = [ m ];

        while (l = read(f)) {
            local t;

            if (t = regex(l, '/^[A-Za-z0-9_\.-]+:/'))
                push(r, m ~ ' ' ~ sregex(t, '/:/', ''));
        }

        close(f);
    }
    else
    if ((f = mp.active.syntax.build_targets) != NULL) {
        r = f->map(sub (e) { sprintf(e, mp.active.name); });
    }

    return r;
}


sub mp.build_execute(target)
{
    local log = mp.open('<build output>');

    log.txt.lines = [];
    log.txt.y = 0;

    /* pipe through make */
    local p;
    if ((p = popen(target, 'r')) != NULL) {
        local l;

        while (l = read(p))
            log->append(mp.chomp(l));

        pclose(p);

        log.txt.mod = 0;
        log.read_only = 1;

        log.syntax = mp.get_syntax('make_output');

        log->move_eof();
        log->move_bol();

        /* set the last search regex to match file:line strings,
            so that calling seek-next and seek-prev moves there */
        mp.last_search = '/^[a-z\.\_0-9\/-]+:[0-9]+:/m';

        mp.redraw();
    }
    else
        mp.alert(sprintf(L("Error executing '%s'"), target));
}
