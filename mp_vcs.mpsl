/*

    Minimum Profit 5.x
    A Programmer's Text Editor

    Version Control System support

    Copyright (C) 1991-2011 Angel Ortega <angel@triptico.com>

    This program is free software; you can redistribute it and/or
    modify it under the terms of the GNU General Public License
    as published by the Free Software Foundation; either version 2
    of the License, or (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.

    http://www.triptico.com

*/

/** data **/

mp.vcs = [
    {
        id:     'git',
        check:  '.git',
        opts:   [ 'git diff', 'git status' ]
    },
    {
        id:     'SVN',
        check:  '.svn',
        opts:   [ 'svn diff', 'svn status' ]
    }
];

/** editor actions **/

mp.actions['vcs'] = sub (d) {
    local v = NULL;

    foreach (n, mp.vcs) {
        if (stat(n.check) != NULL) {
            v = n;
            break;
        }
    }

    if (v == NULL)
        mp.alert(L("This directory is not under a supported version control system"));
    else {
        local l = v.opts;

        local t = mp.form(
            [
                {
                    label:  sprintf(L("Available %s commands"), v.id),
                    type:   'list',
                    list:   l,
                    value:  0
                }
            ]
        );

        if (t != NULL) {
            local cmd = l[t[0]];

            local log = mp.open(sprintf(L("<%s output>"), cmd));

            log.txt.lines   = [];
            log.txt.y       = 0;

            mp.actions.exec_command(log, cmd);
            mp.detect_syntax(log);

            mp.move_bof(log);
            log.txt.mod = 0;
        }
    }
};

/** default key bindings **/

mp.keycodes['ctrl-p'] = 'vcs';

/** action descriptions **/

mp.keycodes['vcs'] = LL("Open the Version Control submenu");

/** code **/

